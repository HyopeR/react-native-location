apply plugin: 'com.android.library'

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

buildscript {
  repositories {
    mavenCentral()
    google()
  }

  dependencies {
    classpath("com.android.tools.build:gradle:7.3.1")
  }
}

android {
  def agpVersion = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION
  if (agpVersion.tokenize('.')[0].toInteger() >= 7) {
    namespace "com.github.reactnativecommunity.location"
  }

  compileSdkVersion safeExtGet('compileSdkVersion', 34)
  buildToolsVersion safeExtGet("buildToolsVersion", "34.0.0")

  defaultConfig {
    minSdkVersion safeExtGet("minSdkVersion", 16)
    targetSdkVersion safeExtGet('targetSdkVersion', 34)
  }

  lintOptions{
    abortOnError false
  }

  packagingOptions {
    // For some reason gradle only complains about the duplicated version of libreact_render libraries
    // while there are more libraries copied in intermediates folder of the lib build directory, we exclude
    // only the ones that make the build fail (ideally we should only include libsafeareacontext_modules but we
    // are only allowed to specify exclude patterns)
    exclude "**/libreact_render*.so"
  }
}

repositories {
  mavenCentral()
  mavenLocal()
  maven {
    // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
    url "$rootDir/../node_modules/react-native/android"
  }
  maven {
    // Android JSC is installed from npm
    url "$rootDir/../node_modules/jsc-android/dist"
  }
  google()
}

def playServicesVersion = safeExtGet("playServicesVersion", "18.2.0")

dependencies {
  implementation "com.facebook.react:react-native:+"
  implementation "com.google.android.gms:play-services-base:$playServicesVersion"
  implementation "com.google.android.gms:play-services-location:21.0.1"
}
